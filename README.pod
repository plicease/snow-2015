=pod

The boss, Mr. Claus asked me to whip up some decorations, and since this 
is a modern outfit with the largest toy and child database in the world 
cutting some paper snowflakes just wasn't going to cut it.  I decoded to 
write a little Perl pogram to render some three dimensional snowflakes.  
To that end, I started with a model to represent the physical 
characteristics of each snowflake, using a Moose role.

 package SnowflakeModel;
 
 use Moose::Role;
   
 has num_twigs => (
   is => 'ro', 
   isa => 'Num', 
   default => sub { int(rand 4) + 3 },
 );
 
 has pinkie_length => (
   is => 'ro',
   isa => 'Num',
   default => sub { 0.5 + (rand)/2.0 }
 );
 
 has branch_length => (
   is => 'ro',
   isa => 'Num',
   default => sub { 1.0 + (rand) },
 );
   
 has segments => (
   is => 'ro',
   isa => 'Int',
   default => sub { int(rand 4) + 2 },
 );
   
 has $_ => (is => 'rw', isa => 'Num', default => 0.0 )
   for qw( x y z );
   
 has $_ => (is => 'rw', isa => 'Num', default => 0.0 )
   for qw( xspin yspin zspin );
 
 1;

The model keeps track of the number of branches, and twigs (the "leaf" 
node, but this being winter time the leaves have obviouslly fallen) and 
the length of the pinkie twigs which are twigs eminating from the root 
of te snowflake.

The next step was to write some code to draw the snowflake.  After some 
tweaking, I decided on a recursive algorithm using OpenGL.

 package SnowflakeDraw;
 
 use 5.010;
 use Moose::Role;
 use GL;
 use GLUT;
   
 requires qw( 
   num_twigs pinkie_length branch_length segments
   x y z xspin yspin zspin
 );
   
 sub draw_twig {
   my($self, $base, $height, $sides) = @_;
   glPushMatrix();
     glRotated(-90.0, 1.0, 0.0, 0.0);
     glutSolidCone($base, $height, $sides, 5);
   glPopMatrix();
 }
   
 sub draw_branch {
   my($self, $size, $left, $segments) = @_;
   $segments //= $self->segments;
     
   glPushMatrix();
     glPushMatrix();
       glRotated(30, 0.0, 0.0, 1.0);
       $self->draw_branch($size/1.5, $size, int($segments/2)) if $segments > 1;
       $self->draw_twig(0.025, $size, 6);
       glRotated(-60, 0.0, 0.0, 1.0);
       $self->draw_branch($size/1.5, $size, int($segments/2)) if $segments > 1;
       $self->draw_twig(0.025, $size, 6);
     glPopMatrix();
     glTranslated(0.0, $left/($segments*.75), 0.0);
     $self->draw_branch(
       $size / 1.5,
       $left-$left/($segments*.75), 
       int($segments-1)
     ) if $segments > 1;
   glPopMatrix();
 }
 
 sub draw {
   my($self) = @_;
     
   glPushMatrix();
     glTranslated($self->x, $self->y, $self->z);
     glRotated($self->xspin, 1.0, 0.0, 0.0);
     glRotated($self->yspin, 0.0, 1.0, 0.0);
     glRotated($self->zspin, 0.0, 0.0, 1.0);
       
     glEnable(GL_COLOR_MATERIAL);
     glEnable(GL_BLEND);
     glColor4d(0.60, 0.86, 1.0, 0.35);
 
     for(1..$self->num_twigs) {
       glPushMatrix();
         glRotated(360/$self->num_twigs*($_-1), 0.0, 0.0, 1.0);
         $self->draw_branch(
           $self->pinkie_length/1.25, 
           $self->branch_length/1.25,
         );
         $self->draw_twig(0.05, $self->branch_length/1.25, 12);
       glPopMatrix();
     }
       
     glPushMatrix();
       glRotated(90, 1.0, 0.0, 0.0);
       $self->draw_branch($self->pinkie_length/1.25, $self->branch_length/1.25);
       glRotated(90, 0.0, 1.0, 0.0);
       $self->draw_branch($self->pinkie_length/1.25, $self->branch_length/1.25);
       $self->draw_twig(0.05, $self->branch_length/1.25, 12);
           
       glRotated(-180, 1.0, 0.0, 0.0);
       $self->draw_branch($self->pinkie_length/1.25, $self->branch_length/1.25);
       glRotated(90, 0.0, 1.0, 0.0);
       $self->draw_branch($self->pinkie_length/1.25, $self->branch_length/1.25);
       $self->draw_twig(0.05, $self->branch_length/1.25, 12);
     glPopMatrix();
 
     glDisable(GL_BLEND);
     glDisable(GL_COLOR_MATERIAL);
   glPopMatrix();
 }
 
 1;

Now I can combine the model and the draw logic into a single Moose class.

 package Snowflake;
 
 use Moose;
 
 with 'SnowflakeModel';
 with 'SnowflakeDraw';
 
 sub spin {
   my($self, $xdelta, $ydelta, $zdelta) = @_;
   $self->xspin($self->xspin+$xdelta);
   $self->yspin($self->yspin+$ydelta);
   $self->zspin($self->zspin+$zdelta);
 }
 
 1;

Now if our snowflakes need to work with vendors that don't support 
OpenGL, we can swap out different draw roles to replace this OpenGL one.

In order to use The GL functions from Perl, I wrote some partial GL 
bindings for Perl using FFI.  FFI is a technique for calling code in 
dynamic libraries from a scripting language like Perl without writing 
XS, which is tied closely to the internal implementation of Perl itself.

We here at North Pole Inc. are big into FFI because we don't have many 
XS programmers, but we often need to call into libraries written in 
other languages which don't have XS bindings yet.  Last year we were 
using L<FFI::Raw>, which at the time was the only practical FFI 
available on CPAN.

Over our long summer break this year, an acquatic mammal who was 
visiting from down under introduced us to L<FFI::Platypus>.  Here are my 
bindings for the base OpenGL library:

 package GL;
 
 use strict;
 use warnings;
 use FFI::Platypus;
 use FFI::CheckLib qw( find_lib_or_die );
 use base qw( Exporter );
   
 use constant {
   GL_SMOOTH              => 0x1D01,
   GL_DEPTH_TEST          => 0x0B71,
   GL_BLEND               => 0x0BE2,
   GL_TRUE                => 0,
   GL_FALSE               => 0,
   GL_LIGHTING            => 0x0B50,
   GL_LIGHT0              => 0x4000,
   GL_DIFFUSE             => 0x1201,
   GL_POSITION            => 0x1203,
   GL_COLOR_BUFFER_BIT    => 0x00004000,
   GL_DEPTH_BUFFER_BIT    => 0x00000100,
   GL_CULL_FACE           => 0x0B44,
   GL_PROJECTION          => 0x1701,
   GL_MODELVIEW           => 0x1700,
   GL_SRC_ALPHA           => 0x0302,
   GL_ONE_MINUS_SRC_ALPHA => 0x0303,
   GL_COLOR_MATERIAL      => 0x0B57,
 };
   
 our $ffi = FFI::Platypus->new(
   lib => [
     $^O eq 'darwin' ? (
       "/System/Library/Frameworks/OpenGL.framework/Libraries/libGL.dylib",
       "/System/Library/Frameworks/OpenGL.framework/Libraries/libGLU.dylib",
       "/System/Library/Frameworks/GLUT.framework/GLUT",
     ) : (
       find_lib_or_die(lib => 'glut'),
       find_lib_or_die(lib => 'GLU'),
       find_lib_or_die(lib => 'GL'),
     )
   ],
 );
 
 $ffi->type('unsigned int' => 'GLenum');
 $ffi->type('unsigned char' => 'GLboolean');
 $ffi->type('int' => 'GLint');
 $ffi->type('double' => 'GLdouble');
 $ffi->type('float[]' => 'GLfloat_array');
 $ffi->type('unsigned int' => 'GLbitfield');
 $ffi->type('int' => 'GLsizei');
 
 $ffi->attach( glShadeModel => ['GLenum'] => 'void' );
 $ffi->attach( glEnable     => ['GLenum'] => 'void' );
 $ffi->attach( glDisable    => ['GLenum'] => 'void' );
 $ffi->attach( glPushMatrix => [] => 'void' );
 $ffi->attach( glPopMatrix  => [] => 'void' );
 $ffi->attach( glFlush      => [] => 'void' );
 $ffi->attach( glRotated    => [ 'GLdouble', 'GLdouble', 
                                 'GLdouble', 'GLdouble' ] => 'void' );
 $ffi->attach( glTranslated => ['GLdouble', 'GLdouble', 
                                'GLdouble' ] => 'void' );
 $ffi->attach( glColor4d    => [ 'GLdouble', 'GLdouble', 
                                 'GLdouble', 'GLdouble' ] => 'void' );
 $ffi->attach( glColor3d    => [ 'GLdouble', 'GLdouble', 
                                 'GLdouble' ] => 'void' );
 $ffi->attach( glLightfv    => [ 'GLenum', 'GLenum', 
                                 'GLfloat_array' ] => 'void' );
 $ffi->attach( glClear      => [ 'GLbitfield' ] => 'void' );
 $ffi->attach( glScaled     => [ 'GLdouble', 'GLdouble', 
                                 'GLdouble' ] => 'void' );
 $ffi->attach( glClearColor => ['float', 'float', 
                                'float', 'float' ] => 'void' );
 $ffi->attach( glMatrixMode => [ 'GLenum' ] => 'void' );
 $ffi->attach( glViewport   => [ 'GLint', 'GLint', 
                                 'GLsizei', 'GLsizei' ] => 'void' );
 $ffi->attach( glBlendFunc  => [ 'GLenum', 'GLenum' ] => 'void' );
 $ffi->attach( glLoadIdentity => [] => 'void' );
 $ffi->attach( gluPerspective => ['GLdouble','GLdouble',
                                  'GLdouble','GLdouble'] => 'void' );
 
 our @EXPORT = (grep /^(gl|GL_)/i, keys %GL::);
 
 1;

After the usual module imports, you can see the first thing that I've 
defined a number of constants which are used by OpenGL.  For FFI there 
really isn't any getting around this.  These constants are usually 
defined in C header (.h) files.  So long as the library is stable and 
the implementers are not feeling cruel this can be okay.

The next peice of code creates an instance of L<FFI::Platypus> which we 
will use to create the bindins to the library:

 our $ffi = FFI::Platypus->new(
   lib => [
     $^O eq 'darwin' ? (
       "/System/Library/Frameworks/OpenGL.framework/Libraries/libGL.dylib",
       "/System/Library/Frameworks/OpenGL.framework/Libraries/libGLU.dylib",
       "/System/Library/Frameworks/GLUT.framework/GLUT",
     ) : (
       find_lib_or_die(lib => 'glut'),
       find_lib_or_die(lib => 'GLU'),
       find_lib_or_die(lib => 'GL'),
     )
   ],
 ); 

We need to tell Platypus where to look for functions so we are using 
L<FFI::ChecKLib>, which is a portable way of searching for system 
libraries.  We have a mixed environment of OS X and Linux for toy 
deliveries and testing, so it this had to work on both platforms.  
Although L<FFI::CheckLib> works on both platforms (and others), the 
OpenGL libraries on OS X are part of a "framework", which isn't 
supported by L<FFI::CheckLib> yet, so I hard coded the location of the 
libraries on that platform.

The next part of the code defines the types used by the library:

 $ffi->type('unsigned int' => 'GLenum');

This line defines C<GLenum> used throughout the GL library.  
Although you could just use C<'unsigned int'> throughout your code where 
GLenum is called for, this makes my code more readable, and it is easier 
to fix the type in one place if I make a mistake wiht C<GLenum>.

In general, the type system for Platypus is a bit different than other 
FFI implementations that I've used.  Instead of providing only basic 
system types like "sixteen bit signed integer", Platypus encourages the 
use of language and platform specific types--typically the same types 
that are understood by your library interface specifications (In C the 
header file).  For example C<long> is a 32 or 64 bit signed integer 
depending on your platform and C<char> might be either signed or 
unsigned!  You can even tell Platypus that you are working with Fortran 
or Rust and use the native types of those languages in your bindings.

Finally in the meat of the module we attach the functions from the 
library into the GL namespace:

 $ffi->attach( glEnable => ['GLenum'] => 'void' );
 ...
 glEnable(GL_LIGHT0);

The C<attach> method takes a function defined in the dynamic library and 
attaches it as a real Perl subroutine.  The advantage to this approach 
is that the code that calls our GL library doesn't need to know or care 
that it is using an FFI library or an XS one.

Platypus also supports higher level types such as structs, and arrays:

 $ffi->type('float[]' => 'GLfloat_array');
 ...
 $ffi->attach( glLightfv    => [ 'GLenum', 'GLenum',
                                 'GLfloat_array' ] => 'void' );
 ...
 glLightfv(GL_LIGHT0, GL_DIFFUSE, [.9, .9, 1.0, 1.0]);
 glLightfv(GL_LIGHT0, GL_POSITION, [1.0, 1.0, 1.0, 0.0]);

Many FFI implementations require you to create an array object and pass 
that in as an argument.  Unfortunately there is an overhead to creating 
a objects for single use calls like this, and it also reveals too much 
of the implementation to your caller.

Finally, this module uses Exporter to export all of its functions to the 
caller's namespace:

 use base qw( Exporter );
 ...
 our @EXPORT = (grep /^(gl|GL_)/i, keys %GL::);

Keeping C<@EXPORT> in sync with functions as we add them is very tedius, 
so instead we use C<%GL::> which is a special hash that keeps track of 
the data structures (including subroutines) in the namespace.  It has to 
be put at the bottom of the file because otherwise %GL:: won't be 
populated completely, until after all of the C<attach> methods are 
called.

The last piece of code is the Glut bindings.  Glut provides a simple and 
portable OpenGL focused event loop for applications that do not require 
anything more complicated.

# GLUT.pm

A few things are interesting here.  Firstly, I use the same C<$ffi> 
object here, mainly because Glut uses a number of GL types.

 my $ffi = $GL::ffi;

Second, the C<glutInit()> takes an array of C strings as its second 
argument.

 $ffi->load_custom_type('FFI::Platypus::Type::StringArray' => 'string_array');

 $ffi->attach( glutInit => ['int*', 'string_array' ] => 'void' => sub {
   my($xsub, @args) = @_;
   my $size = scalar @args;
   $xsub->(\$size, \@args);
 });

Platypus' type system provides most of the common basic types that you 
will need.  A few more specalized types, like arrays of C strings 
require the use of custom types written in Perl.  Here we are using 
L<FFI::Platypus::Type::StringArray> which is frequently useful, but not 
part of the Platypus core.

Also interesting here is that we are using a wrapper (specified as the 
last argument in the attach call) around the subroutine created by 
Platypus.  This is an interfacing issue, as in C if you have arrays that 
can be of varying lengths you need to pass in the size of the array as a 
separate parameter, but in Perl arrays know how many they contain.

Also the size of the array is passed in as a pointer an integer so that 
Glut can alter its value.  Although in this particular case we ignore 
any changes made by Glut, the scalar C<$size> IS updated if C<glutInit> 
makes a change to its value.  In many use cases this is very useful.

The last thing that I want to point out is the use of closures:

 $ffi->attach( glutDisplayFunc => [ '()->void' ] => 'void' => sub {
   my($xsub, $callback) = @_;
   state $closure = $ffi->closure($callback);
   $xsub->($closure);
 });

Or calling back into Perl from C.  This is extremly usful, but it is 
also a little tricky, because of the different ways that Perl and C 
manage memory.  Essentially it comes down to this, in C you must 
explicitly allocate and deallocate all memory.  The convention is that 
the module that allocates a particular object will be responsible for 
deallocating it.  In Perl the language itself takes responsability for 
deallocating objects when they are no longer used anywhere in the Perl 
runtime.  Unfortunately when we create a closure and pass it into Glut 
code, there is no way for Perl to keep track of wether or not the 
closure is still in use by Glut!  In this case I save the closure into a 
C<state> variable, which will keep it in scope for the life of the 
program (or until C<glutDisplayFunc> is called again).

Now, finally I have three pretty snowflakes for the holiday party.

=begin html

<img src="screenshot800.png"/>

=end html

My coworker android friend Mr. Lore also enjoyed it reminded him of his 
friend the crystalline entity.

=cut
